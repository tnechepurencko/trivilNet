модуль контроль

импорт "стд::вывод"
//импорт "стд::строки"

импорт "трик/асд"
импорт "трик/основа"

фн (кон: Контроль) проверить константное выражение(выр: асд.Выражение) {
	если кон.это константное выражение(выр) {
		вернуть
	}
	основа.добавить ошибку(выр.поз, "СЕМ-ОШ-КОНСТ-ВЫРАЖЕНИЕ")
}

фн (кон: Контроль) это константное выражение(выр: асд.Выражение): Лог {
	выбор пусть тек: тип выр {
	когда асд.Литерал:
		вернуть истина
	когда асд.Преобразовать:
		вернуть тек.сделано
	когда асд.ОперандИдент:
		вернуть кон.это константа(тек.объект^)
	когда асд.Доступ:
		вернуть тек.Л = пусто & кон.это константа(тек.объект^)
	}
	вернуть ложь
}

фн (кон: Контроль) это константа(объект: асд.Узел): Лог {
	выбор тип объект {
	когда асд.ОписаниеКонстанты, асд.Функция:
		вернуть истина
	другое
		вернуть ложь
	}
}

фн (кон: Контроль) вычислить целую константу(выр: асд.Выражение): Цел64 {
	выбор пусть тек: тип выр {
	когда асд.Литерал:
		если тек.вид = асд.Лит-Цел {
			вернуть тек.цел
		} иначе если тек.вид = асд.Лит-Слово {
			вернуть тек.слово(:Цел64) //TODO: проверить на диапазон
		}
		авария("wrong literal kind")
	когда асд.ОперандИдент:
		пусть к = тек.объект(:асд.ОписаниеКонстанты)

		вернуть кон.вычислить целую константу(к.значение^)
	}
	авария("wrong конст expression")
}
