модуль парсер

импорт "стд::вывод"
импорт "трик/асд"
импорт "трик/лексер"
импорт "трик/основа"


фн (п: Парсер) указание-типа(): асд.Тип {

	пусть мб-тип := ложь
    пусть мб-поз = п.поз

	если п.лек = лексер.МБ {
        мб-тип := истина
		п.взять()
	}

    пусть поз-типа = п.поз

	пусть имя-типа := п.идент()
    пусть имя-модуля := ""

	если п.лек = лексер.ТОЧКА {
		п.взять()
        имя-модуля := имя-типа
		имя-типа := п.идент()
	}

    пусть Т = асд.УказТипа{
        поз: поз-типа,
        имя-типа: имя-типа,
        имя-модуля: имя-модуля,
    }
    
    если мб-тип {
        вернуть асд.ТипМБ{
            поз: мб-поз,
            Т: Т,
        }
    }

	вернуть Т
}

фн (п: Парсер) описание-типа(): асд.ОписаниеТипа {

	п.взять()
    
    пусть поз = п.поз
	пусть имя = п.идент()
    пусть экспорт = п.экспорт()

	п.нужна лексема(лексер.РАВНО)
    
    пусть оп = асд.ОписаниеТипа{
        поз: поз,
        имя: имя,
        Т: п.определение-типа(),
        экспорт: экспорт,
    }
    
	вернуть оп
}

фн (п: Парсер) определение-типа(): асд.Тип {

	выбор п.лек {
    когда лексер.КВАД-Л:
        вернуть п.тип-вектор()
	когда лексер.КЛАСС:
		вернуть п.тип-класс()
	другое
		вернуть п.указание-типа()
	}
}

фн (п: Парсер) тип-вектор(): асд.ТипВектор {

    пусть поз = п.поз
	п.взять()
	п.нужна лексема(лексер.КВАД-П)
    
    пусть Т = асд.ТипВектор{
        поз: поз,
        Т-элемента: п.указание-типа(),
    }

	вернуть Т
}

//== класс

фн (п: Парсер) тип-класс(): асд.ТипКласс {

	пусть Т = асд.ТипКласс{
        поз: п.поз,
		//Members:  make(map[Строка]асд.Описание),
	}

	п.взять()

	если п.лек = лексер.КРУГ-Л {
		п.взять()
		Т.Т-базовый := п.указание-типа()
		п.нужна лексема(лексер.КРУГ-П)
	}

	п.нужна лексема(лексер.ФИГ-Л)

	пока п.лек # лексер.ФИГ-П & п.лек # лексер.КОН-ФАЙЛ {

		пусть поле = п.поле-класса()
        Т.поля.добавить(поле)

		если п.лек = лексер.ФИГ-П {
			прервать
		}
		п.разделитель()
	}

	п.нужна лексема(лексер.ФИГ-П)

	вернуть Т
}

фн (п: Парсер) поле-класса(): асд.Поле {

	пусть поле = асд.Поле{
        поз: п.поз,
	}

	поле.имя := п.идент()
    поле.экспорт := п.экспорт()

	если п.лек = лексер.ДВОЕТОЧИЕ {
		п.взять()
		поле.Т := п.указание-типа()
	}

	если п.лек = лексер.РАВНО {
		поле.одно-присваивание := истина
		п.взять()
	} иначе если п.лек = лексер.ПРИСВОИТЬ {
		п.взять()
	} иначе {
		п.ошибка("ПАР-ПЕРЕМ-ИНИТ")
		вернуть поле
	}

	если п.лек = лексер.ПОЗЖЕ {
		поле.задать-позже := истина
		п.взять()
	} иначе {
		поле.значение := п.выражение()
	}

	вернуть поле
}
