// Области действия
модуль асд

импорт "стд::вывод"

// Область действия
тип Область* = класс {
	выше*: мб Область = пусто
	имена*: Словарь = позже // [Строка]Описание
}

пусть универсум = Область{
    имена: Словарь{}
}

// Встроенные типы, видимые разработчику
пусть тБайт = ТипВстроенный{поз: 0, имя: "Байт"}
пусть тЦел64 = ТипВстроенный{поз: 0, имя: "Цел64"}
пусть тВещ64 = ТипВстроенный{поз: 0, имя: "Вещ64"}
пусть тСлово64 = ТипВстроенный{поз: 0, имя: "Слово64"}
пусть тЛог = ТипВстроенный{поз: 0, имя: "Лог"}
пусть тСимвол = ТипВстроенный{поз: 0, имя: "Символ"}
пусть тСтрока = ТипВстроенный{поз: 0, имя: "Строка"}
пусть тСтрока8 = ТипВстроенный{поз: 0, имя: "Строка8"}

пусть тПусто =  ТипВстроенный{поз: 0, имя: "Пусто" } //

// Встроенные типы, не видимые разработчику
пусть тПолиморфный  = ТипВстроенный{поз: 0, имя: "Полиморфный" }
пусть тНетЗначения = ТипВстроенный{поз: 0, имя: "нет значения" }

фн ТБайт*(): ТипВстроенный { вернуть тБайт }
фн ТЦел64*(): ТипВстроенный { вернуть тЦел64 }
фн ТВещ64*(): ТипВстроенный { вернуть тВещ64 }
фн ТСлово64*(): ТипВстроенный { вернуть тСлово64 }
фн ТЛог*(): ТипВстроенный { вернуть тЛог }
фн ТСимвол*(): ТипВстроенный { вернуть тСимвол }
фн ТСтрока*(): ТипВстроенный { вернуть тСтрока }
фн ТСтрока8*(): ТипВстроенный { вернуть тСтрока8 }

фн ТипПолиморфный*(): ТипВстроенный { вернуть тПолиморфный }
фн ТипНетЗначения*(): ТипВстроенный { вернуть тНетЗначения }
фн ТипПусто*(): ТипВстроенный { вернуть тПусто }

// Стандартные функции
конст * (
	СтдДлина  = "длина"
	СтдТег      = "тег"
	СтдНечто  = "нечто"
)

// Методы вектора
конст * (
	ВекторДобавить = "добавить"
	ВекторВыделить = "выделить" // метод не сделан, используется в композите вектора
)

//пусть методыВектора = vectorMethods map[Строка]*StdFunction

//=== добавление в универсум

фн добавить встроенный(т: ТипВстроенный) {
    пусть оп = ОписаниеТипа{поз: 0}
    оп.имя := т.имя
    оп.Т := т
    
    универсум.имена.добавить(т.имя, оп)
}

фн добавить константу(имя: Строка, Т: Тип, значение: Выражение) {
	пусть оп = ОписаниеКонстанты{
        поз: 0,
        имя: имя,
        Т: Т,
        значение: значение,
	}

    универсум.имена.добавить(имя, оп)
}

фн добавить стандартную функцию(имя: Строка) {
    пусть оп = СтдФункция{
        поз: 0,
        имя: имя,
        Т: тНетЗначения, // нужно ли это?
    }

    универсум.имена.добавить(имя, оп)
}

//== методы для всех векторов

тип СтдФункции = []СтдФункция

пусть методы-векторов = СтдФункции[]

фн добавить метод векторов(имя: Строка) {
	пусть ф = СтдФункция{
        поз: 0,
        имя: имя,
        Т: тНетЗначения,
        метод?: истина
    }
    
    методы-векторов.добавить(ф)
}

фн найти метод векторов*(имя: Строка): мб  СтдФункция {
    цикл ф среди методы-векторов {
        если ф.имя = имя { вернуть ф }
    }
	вернуть пусто
}

//==== области

фн новая область*(выше: Область): Область {
	вернуть Область{
		выше: выше,
		имена: Словарь{},
	}
}

фн показать области*(заголовок: Строка, о: Область) {
	если заголовок # "" {
        вывод.ф("$;\n", заголовок)
	}

	пусть глубина := 0
	пока истина {
		глубина++
		вывод.ф("область $; ===>\n", глубина)
        
        пусть и = о.имена.начать()
        пока и.элемент? # пусто {
            вывод.ф(" $;", и.элемент?^.ключ)
            и.следующий()
		}
		если о.имена.число элементов() > 0 {
            вывод.ф("\n")
		}

        надо о.выше # пусто иначе прервать
		о := о.выше^
	}
	вывод.ф("<=== завершение областей\n")
}

вход {
    // добавление в универсум
    добавить встроенный(тБайт)
    добавить встроенный(тЦел64)
    добавить встроенный(тВещ64)
    добавить встроенный(тСлово64)
    добавить встроенный(тЛог)
    добавить встроенный(тСимвол)
    добавить встроенный(тСтрока)
    добавить встроенный(тСтрока8)
    добавить встроенный(тПусто)

    добавить константу("ложь", тЛог, ЛогЛитерал{поз: 0, значение: ложь})
    добавить константу("истина", тЛог, ЛогЛитерал{поз: 0, значение: истина})

    добавить константу("пусто", тПусто, Литерал{поз: 0, вид: Лит-Пусто, Т: тПусто})

	добавить стандартную функцию(СтдДлина)
	добавить стандартную функцию(СтдТег)
	добавить стандартную функцию(СтдНечто)

	добавить метод векторов(ВекторДобавить)
	//добавить метод векторов(VectorFill)

    //показать области("", универсум)
}