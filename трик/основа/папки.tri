// Работа со служебными папками
модуль основа

//импорт "стд::вывод"
импорт "стд::строки"
импорт "стд::файлы"
импорт "стд::контейнеры/словарь/стр-стр"

конст (
	разделитель-хранилища = "::"
	имя-стандартного  = "стд"
	имя-папки-настроек = "config"
	имя-папки-кода = "./_си"
)

пусть корни = стр-стр.Словарь{} // [Строка]Строка

фн собрать путь настройки*(имя-файла: Строка): Строка {
    вернуть файлы.собрать путь(файлы.путь к папке программы(),  имя-папки-настроек, имя-файла)
}

фн путь к папке runtime*(): Строка {
    вернуть файлы.собрать путь(файлы.путь к папке программы(), "runtime")
}

// Выдает или нормализованный путь или сообщение об ошибке
фн нормализовать путь*(путь: Строка, нормализованный-путь := Строка, ошибка := мб Строка) {

    нормализованный-путь := ""
    ошибка := пусто
    
    пусть имя := ""
    пусть путь-хранилища := ""    
    если строки.разделить(путь, разделитель-хранилища, имя, путь-хранилища) & длина(имя) # 0 {
        пусть эл = корни.найти(имя)
        если эл = пусто {
            ошибка := строки.ф("путь для кодовой базы '$;' не задан", имя)
            вернуть
        }
        нормализованный-путь := файлы.собрать путь(эл^.значение, путь-хранилища)
        вернуть
    }
    
    пусть ф = файлы.новый файл(путь)

    нормализованный-путь := ф.абсолютный путь()
    если ф.ошибка?() # пусто {
        нормализованный-путь := ""
        ошибка := ф.ошибка?()^.сообщение()
    }
}

фн подготовить папку кода*(): Строка {
    // временно, вместо создания папки
    пусть ф = файлы.новый файл(имя-папки-кода)
    если ~ф.это папка?() {
        если ~ф.создать папку() {
            авария("временно: не удалось создать папку кода")
        }
    }
    вернуть имя-папки-кода
}

фн подготовить хранилища() {

    пусть стд-путь = файлы.собрать путь(файлы.путь к папке программы(), имя-стандартного)

    пусть ф = файлы.новый файл(стд-путь)
    если ф.это папка?() {
        корни.добавить(имя-стандартного, стд-путь)
    }    

	// TODO: прочитать файл со списком кодовых баз
}
