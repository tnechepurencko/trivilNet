модуль файлы

импорт "стд::строки"

тип Байты* = []Байт

тип Запрос = класс {
    код-ошибки: мб Строка := пусто
}

//==== Системный интерфейс

фн sysapi_fread(запрос: Запрос, имя-файла: Строка): мб Байты @внеш("sysapi":"rt_sysapi")
фн sysapi_fwrite(запрос: Запрос, имя-файла: Строка, байты: Байты) @внеш("sysapi":"rt_sysapi")
фн sysapi_is_dir(запрос: Запрос, имя-файла: Строка): Лог @внеш("sysapi":"rt_sysapi")
фн sysapi_dirnames(запрос: Запрос, имя-файла: Строка): Строки @внеш("sysapi":"rt_sysapi")
фн sysapi_exec_path(): Строка @внеш("sysapi":"rt_sysapi")

//====

тип Файл* = класс {
    заданный-путь: Строка := позже
    ошибка: мб Ошибка := пусто
}

// Выдает файл с заданным путем. НЕ делает никаких файловых операций
фн новый файл*(путь: Строка): Файл {
    вернуть Файл{заданный-путь: путь}
}

фн (ф: Файл) ошибка?*(): мб Ошибка {
    вернуть ф.ошибка
}

фн (ф: Файл) путь*(): Строка {
    вернуть ф.заданный-путь
}

// ==== чтение/запись ====

// Читает целиком файл, расположенный по заданному пути
фн (ф: Файл) прочитать*(): Байты {

    пусть запрос = Запрос{}
    
    пусть байты = sysapi_fread(запрос, ф.заданный-путь)
    
    если запрос.код-ошибки # пусто {
        ф.ошибка := Ошибка{
            код: запрос.код-ошибки^, 
            текст: строки.ф("прочитать '$;': $;", ф.заданный-путь, запрос.код-ошибки^)
        } 
        вернуть Байты[]
    }
    
    вернуть байты^
}

фн (ф: Файл) записать*(байты: Байты) {

    пусть запрос = Запрос{}
    
    sysapi_fwrite(запрос, ф.заданный-путь, байты)
    
    если запрос.код-ошибки # пусто {
        ф.ошибка := Ошибка{
            код: запрос.код-ошибки^, 
            текст: строки.ф("записать '$;': $;", ф.заданный-путь, запрос.код-ошибки^)
        } 
        вернуть
    }
}

// ==== атрибуты файла ====

фн (ф: Файл) это папка?*(): Лог {

    пусть запрос = Запрос{}
    пусть ответ = sysapi_is_dir(запрос, ф.заданный-путь)

    если запрос.код-ошибки # пусто {
        ф.ошибка := Ошибка{
            код: запрос.код-ошибки^, 
            текст: строки.ф("это папка? '$;': $;", ф.заданный-путь, запрос.код-ошибки^)
        } 
        вернуть ложь
    }    

    вернуть ответ
}

// ==== чтение папки ====

// Выдает список имен из папки, расположенной по заданному пути.
// Список не включает "." и ".."
фн (ф: Файл) имена в папке*(): Строки {
    
    пусть запрос = Запрос{}

    пусть список = sysapi_dirnames(запрос, ф.заданный-путь)

    если запрос.код-ошибки # пусто {
        ф.ошибка := Ошибка{
            код: запрос.код-ошибки^, 
            текст: строки.ф("записать '$;': $;", ф.заданный-путь, запрос.код-ошибки^)
        } 
        вернуть Строки[]
    }

    вернуть список
}
