// Настройка:
//  тип Ключ = <тип>
//  тип Значение = <тип>
//  фн хеш(к: Ключ): Цел64 {}
//  конст размер = 1021
модуль словарь
осторожно

//импорт "стд::вывод"

тип Элемент* = класс {
    ключ*: Ключ = позже
    значение*: Значение  := позже
}

тип Элементы? = []мб Элемент

тип Словарь* = класс {
    элементы = Элементы?[длина: размер, *: пусто] 
    кол-во: Цел64 := 0
}

фн (с: Словарь) элементов*(): Цел64 {
    вернуть с.кол-во
}

фн (с: Словарь) добавить*(к: Ключ, з: Значение) {

    надо с.кол-во < длина(с.элементы) * 2 / 3 
    иначе авария("слишком много элементов в таблице")

    пусть № := хеш(к) % длина(с.элементы)
    пока с.элементы[№] # пусто {
        пусть э = с.элементы[№]^
        если э.ключ = к {
            э.значение := з
            вернуть
        }

        № := (№ + 1) % длина(с.элементы)
    }

    с.элементы[№] := Элемент{ключ: к, значение: з}
    с.кол-во++
}

фн (с: Словарь) найти*(к: Ключ): мб Элемент {

    пусть № := хеш(к) % длина(с.элементы)
    пока с.элементы[№] # пусто {
        пусть э = с.элементы[№]^
        если э.ключ = к {
            вернуть э
        }

        № := (№ + 1) % длина(с.элементы)
    }
    вернуть пусто
}

//==== итератор

тип Итератор = класс {
    элемент?*: мб Элемент := пусто
    словарь: Словарь = позже
    № := 0
}

фн (с: Словарь) начать*(): Итератор {
    пусть и = Итератор{словарь: с}
    и.следующий()
    вернуть и
}

фн (и: Итератор) следующий*() {
    и.элемент? := пусто
    пусть с = и.словарь

    пока и.№ < длина(с.элементы) {
        если с.элементы[и.№] # пусто {
            и.элемент? := с.элементы[и.№]
            и.№++
            вернуть
        }
        и.№++
    }
}

//=====

тип Ключ = Строка
тип Значение = Строка

фн хеш(к: Строка): Цел64 {
    пусть с8 = к(:осторожно Строка8)
    пусть № := 0
    пусть рез := 0
    пока № < длина(с8) {
        рез := (рез * 127 + с8[№](:Цел64)) % размер 
        №++
    }
    вернуть рез
}

конст размер = 1021
