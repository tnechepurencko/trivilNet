модуль json

импорт "стд::строки"

тип Строки = []Строка

фн сохранить*(у: Узел): Строка {

    пусть сб = строки.Сборщик{}

    выбор пусть тек: тип у {
    когда Ошибка: 
            сб.ф("ошибка в позиции $;: $;", тек.позиция, тек.сообщение) 
    когда Число: 
        если тек.целое? { 
            сб.ф("$;", тек.цел) 
        } иначе {
            сб.ф("$;", тек.вещ) 
        }
    когда Стр: 
        сб.ф("\"$;\"", тек.строка) 
    когда Массив: 
        пусть список = Строки[]
        цикл у среди тек.узлы {
            список.добавить(сохранить(у))
        }
        сб.ф("[$;]", строки.соединить(", ", список...) )
    когда Запись: 
        пусть список = Строки[]
        цикл п среди тек.пары {
            список.добавить(строки.ф("\"$;\":$;", п.имя, сохранить(п.значение)))
        }
        сб.ф("{$;}", строки.соединить(", ", список...) )
    другое
        авария("!!")
    }

    вернуть сб.строка()
}